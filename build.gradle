import io.github.bonigarcia.wdm.PhantomJsDriverManager

buildscript {
    ext {
        springBootVersion = '1.4.0.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.4.0'
        classpath 'io.github.bonigarcia:webdrivermanager:1.5.0'
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'idea'
apply plugin: 'com.google.osdetector'

jar {
    baseName = 'airent'
    version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven { url "https://repo.boundlessgeo.com/main" }
    maven { url "http://download.osgeo.org/webdav/geotools/" }
    mavenCentral()
    jcenter()
}

def javaCppClassifier = osdetector.classifier.replace 'osx', 'macosx'

dependencies {
    // --- spring ---
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.thymeleaf:thymeleaf-spring4:3.0.1.RELEASE'
    compile 'org.thymeleaf:thymeleaf:3.0.1.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-security'

    // --- database ---
    compile 'org.springframework:spring-jdbc'
    compile 'org.mybatis:mybatis-spring:1.3.0'
    compile 'org.mybatis:mybatis:3.4.1'
    runtime 'com.h2database:h2:1.4.192'
    compile 'org.postgresql:postgresql:9.4.1212'
    compile 'org.apache.commons:commons-dbcp2:2.1.1'
    compile "org.flywaydb:flyway-core:4.0.3"

    // --- parsing ---
    compile 'io.github.bonigarcia:webdrivermanager:1.5.0'
    compile 'org.seleniumhq.selenium:selenium-support:3.0.1'
    compile "com.codeborne:phantomjsdriver:1.3.0"
    compile 'com.squareup.okhttp3:okhttp:3.4.2'
    compile 'org.eclipse.jetty.websocket:websocket-client:9.4.0.M1'
    compile 'org.jsoup:jsoup:1.10.1'
    compile 'org.bytedeco.javacpp-presets:tesseract:3.04.01-1.2'
    compile 'org.bytedeco.javacpp-presets:leptonica:1.73-1.2'
    compile "org.bytedeco.javacpp-presets:tesseract:3.04.01-1.2:$javaCppClassifier"
    compile "org.bytedeco.javacpp-presets:leptonica:1.73-1.2:$javaCppClassifier"
    compile "org.bitcoinj:orchid:1.2.1"

    // --- common ---
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'commons-io:commons-io:2.5'
    compile 'org.geotools:gt-geojson:16.0'

    // --- tests ---
    testCompile 'org.springframework.boot:spring-boot-test'
    testCompile 'org.springframework:spring-test'
    testCompile 'org.testng:testng:6.9.12'
}

configurations {
    all*.exclude group: 'org.bytedeco', module: 'javacpp-presets'
    all*.exclude group: 'junit', module: 'junit'
}

task downloadDependencies(type: Exec) {
    PhantomJsDriverManager.getInstance().setup("2.1.1")
    configurations.testRuntime.files
    commandLine 'echo', 'Downloaded all dependencies'
}

test {
    useTestNG()

    testLogging {
        events "started", "skipped", "failed", "passed"
        exceptionFormat "short"

        // remove standard output/error logging from --info builds
        // by assigning only 'failed' and 'skipped' events
        info.events = ["failed", "skipped"]
    }

    minHeapSize = "64m"
    maxHeapSize = "128m"
}


