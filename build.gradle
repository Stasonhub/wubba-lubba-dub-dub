import com.eriwen.gradle.css.CssMinifier
import com.eriwen.gradle.js.JsMinifier

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.eriwen:gradle-js-plugin:2.12.0"
        classpath "com.eriwen:gradle-css-plugin:2.12.0"
    }
}

plugins {
    id 'java'
    id "org.springframework.boot" version "1.5.1.RELEASE"
    id 'idea'
}

jar {
    baseName = 'airent'
    version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven { url "https://repo.boundlessgeo.com/main" }
    maven { url "http://download.osgeo.org/webdav/geotools/" }
    mavenCentral()
    jcenter()
}

dependencies {
    // --- spring ---
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.thymeleaf:thymeleaf-spring4:3.0.1.RELEASE'
    compile 'org.thymeleaf:thymeleaf:3.0.1.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-security'

    // --- database ---
    compile 'org.springframework:spring-jdbc'
    compile 'org.mybatis:mybatis-spring:1.3.0'
    compile 'org.mybatis:mybatis:3.4.1'
    runtime 'com.h2database:h2:1.4.192'
    compile 'org.postgresql:postgresql:9.4.1212'
    compile 'org.apache.commons:commons-dbcp2:2.1.1'
    compile "org.flywaydb:flyway-core:4.0.3"

    // --- parsing ---
    compile 'io.github.bonigarcia:webdrivermanager:1.5.0'
    compile 'org.seleniumhq.selenium:selenium-support:3.1.0'
    compile 'org.seleniumhq.selenium:selenium-chrome-driver:3.1.0'
    compile 'net.lightbody.bmp:browsermob-core:2.1.4'
    compile 'com.squareup.okhttp3:okhttp:3.4.2'
    compile 'org.bytedeco.javacpp-presets:tesseract:3.04.01-1.2'
    compile 'org.bytedeco.javacpp-presets:leptonica:1.73-1.2'
    compile "org.bytedeco.javacpp-presets:tesseract:3.04.01-1.2:macosx-x86_64"
    compile "org.bytedeco.javacpp-presets:leptonica:1.73-1.2:macosx-x86_64"
    compile "org.bytedeco.javacpp-presets:tesseract:3.04.01-1.2:linux-x86_64"
    compile "org.bytedeco.javacpp-presets:leptonica:1.73-1.2:linux-x86_64"
    compile "com.twelvemonkeys.imageio:imageio-jpeg:3.3.2"

    // --- sitemap ---
    compile "com.github.dfabulich:sitemapgen4j:1.0.6"

    // --- common ---
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'commons-io:commons-io:2.5'
    compile 'org.geotools:gt-geojson:16.0'

    // --- tests ---
    testCompile 'org.springframework.boot:spring-boot-test'
    testCompile 'org.springframework:spring-test'
    testCompile 'org.testng:testng:6.9.12'
}

configurations {
    all*.exclude group: 'org.bytedeco', module: 'javacpp-presets'
    all*.exclude group: 'junit', module: 'junit'
}

task downloadDependencies(type: Exec) {
    configurations.testRuntime.files
    commandLine 'echo', 'Downloaded all dependencies'
}

test {
    useTestNG() {
        excludeGroups 'complex'
    }

    testLogging {
        events "started", "skipped", "failed", "passed"
        exceptionFormat "short"

        // remove standard output/error logging from --info builds
        // by assigning only 'failed' and 'skipped' events
        info.events = ["failed", "skipped"]
    }

    minHeapSize = "64m"
    maxHeapSize = "128m"
}

/** Web part */
task minifyAllJs {
    doLast {
        def minifier = new JsMinifier()
        def files = fileTree("src/main/resources/static/js")
                .include('**/*.js').exclude('**/*min.js')
        for (File file : files) {
            println "Js minify file ${file.getAbsolutePath()}"
            minifier.minifyJsFile(Collections.singleton(file), [] as Set<File>,
                    "${buildDir}/resources/main/static/js/${file.getName()}" as File,
                    null,
                    null,
                    "QUIET",
                    "SIMPLE_OPTIMIZATIONS")
        }
    }
}

task minifyAllCss {
    doLast {
        def minifier = new CssMinifier()
        def files = fileTree("src/main/resources/static/css")
                .include('**/*.css').exclude('**/*min.css')
        for (File file : files) {
            println "Css minify file ${file.getAbsolutePath()}"
            minifier.minifyCssFile(file,
                    "${buildDir}/resources/main/static/css/${file.getName()}" as File,
                    -1)
        }
    }
}

/ ** Dependencies */
jar.dependsOn minifyAllJs
jar.dependsOn minifyAllCss
